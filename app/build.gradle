plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'

}
if(getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains("huawei")) {
    apply plugin: 'com.huawei.agconnect'
} else {
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.gms.google-services'
}

android {

    flavorDimensions "services"
    productFlavors {
        huawei {
            dimension 'services'
            applicationIdSuffix '.huawei'
        }
        google {
            dimension 'services'
        }
    }

    signingConfigs {
        release {
            Properties keystoreConfig = new Properties()
            keystoreConfig.load(new FileInputStream(file('keystore.config')))

            storeFile file(keystoreConfig['storeFile'])
            storePassword keystoreConfig['storePassword']
            keyPassword keystoreConfig['keyPassword']
            keyAlias keystoreConfig['keyAlias']
        }
    }
    compileSdk 31

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "mr.shtein.buddyandroidclient"
        minSdk 24
        targetSdk 31
        versionCode 5
        versionName "1.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release

        buildConfigField "String", "USER_STORAGE_NAME", "\"user_storage\""
        buildConfigField "String", "KENNEL_STORAGE_NAME", "\"kennel_storage\""
        buildConfigField "String", "DATABASE_STORAGE_NAME", "\"database_storage\""
        buildConfigField "String", "FILTER_STORAGE_NAME", "\"filter_storage\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    sourceSets {
        huawei {
            java {
                srcDirs 'src/huawei/java'
            }
        }
        google {
            java {
                srcDirs 'src/google/java'
            }
        }
    }
}

java  {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.airbnb.android:lottie:4.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    implementation 'jp.wasabeef:picasso-transformations:2.4.0'
    implementation 'ru.tinkoff.decoro:decoro:1.5.0'
    implementation "io.insert-koin:koin-android:3.2.0"

    runtimeOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.mockito:mockito-core:4.7.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'io.github.serpro69:kotlin-faker:1.11.0'
    implementation "com.github.moxy-community:moxy:2.2.2"
    implementation "com.github.moxy-community:moxy-app-compat:2.2.2"
    kapt "com.github.moxy-community:moxy-compiler:2.2.2"
    implementation "com.github.moxy-community:moxy-material:2.2.2"
    implementation "com.github.moxy-community:moxy-ktx:2.2.2"

    googleImplementation 'com.google.android.gms:play-services-location:19.0.1'
    googleImplementation platform('com.google.firebase:firebase-bom:30.3.2')
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.firebase:firebase-analytics'

    huaweiImplementation 'com.huawei.hms:location:6.4.0.300'
    huaweiImplementation 'com.huawei.agconnect:agconnect-core:1.7.1.300'
    huaweiImplementation 'com.huawei.agconnect:agconnect-crash:1.7.1.300'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.google.code.gson:gson:2.10'

    implementation project(path: ':core:network')
    implementation project(path: ':core:model')
    implementation project(path: ':core:data')
    implementation project(path: ':core:navigator')

}